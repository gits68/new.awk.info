---
layout: default
title: "Graph.awk"
tags: [dsl, graph, TODO]
---

# {{ page.title }}

## Contents

+ [Synopsis](#01)
+ [Description](#02)
+ [Code](#03)

	+ [Initialization](#03.01)
	+ [Handling patterns](#03.02)
	+ [Functions](#03.03)

+ [Author](#04)

## Synopsis ## {#01}

{% highlight sh %}
gawk -f graph.awk graphFile
{% endhighlight %}

## Description ## {#02}

A processor for a little language, specialized for graph-drawing.

The code inputs data, which includes a specification of a graph. The
output is data plotted in specified area

For example, here is an input specification:

	label here's some stuff
	bottom ticks 1 5 10 
	left ticks 1 2 10 20
	range 1 1 10 22
	height 10
	width 30
	1 2 *
	2 4 * 
	3 6 *
	4 8 *
	7 14 +
	8 12 +
	9 10 +
	mb 0.9 11 =

It produces the following output:

	      |----------------------|
	20    -                 = =  =
	      |       = =  = =       |
	      =  = =         +  +    |
	10    -                   +  |
	      |    *  *              |
	      |  *                   |
	2     *---------|------------|
	     1         5            10
		 here's some stuff    

## Code ## {#03}

### Initialization ### {#03.01}

Set frame dimensions: height and width; offset for x and y axes.

{% highlight awk %}
BEGIN {                
    ht = 24; wid = 80  
    ox = 6; oy = 2     
    number = "^[-+]?([0-9]+[.]?[0-9]*|[.][0-9]+)" \
                            "([eE][-+]?[0-9]+)?$"
}
{% endhighlight %}

### Handling patterns ### {#03.02}

Skip comments

{% highlight awk %}
/^[ \t]*#/     { next } 
{% endhighlight %}

Simple tags

{% highlight awk %}
$1 == "height" { ht = $2;  next }
$1 == "width"  { wid = $2; next }
$1 == "label"  {                       # for bottom
    sub(/^ *label */, "")
    botlab = $0
    next
}
$1 == "bottom" && $2 == "ticks" {     # ticks for x-axis
    for (i = 3; i &lt;= NF; i++) bticks[++nb] = $i
    next
}
$1 == "left" && $2 == "ticks" {       # ticks for y-axis
    for (i = 3; i &lt;= NF; i++) lticks[++nl] = $i
    next
}
$1 == "range" {                       # xmin ymin xmax ymax
    xmin = $2; ymin = $3; xmax = $4; ymax = $5
    next
}
{% endhighlight %}

Handling numerics.

{% highlight awk %}
$1 ~ number && $2 ~ number {  # pair of numbers
    nd++                      # count number of data points
    x[nd] = $1; y[nd] = $2
    ch[nd] = $3               # optional plotting character
    next
}
$1 ~ number && $2 !~ number { # single number
    nd++                      # count number of data points
    x[nd] = nd; y[nd] = $1; ch[nd] = $2
    next
}
{% endhighlight %}

Line functions, defined by a slope "m" and a y-intercept "b".

{% highlight awk %}
$1 == "mb" {  # m b [mark]
	expand()
    for(i=xmin;i&lt;=xmax;i++) {
		nd++; x[nd]=i; y[nd]=$2*i + $3; ch[nd]=$4 
    }
    next;
}		
{% endhighlight %}

Final case: input error.

{% highlight awk %}
{ print "?? line " NR ": ["$0"]" >"/dev/stderr" }
{% endhighlight %}

Draw the graph

{% highlight awk %}
END { expand();   frame(); ticks(); label(); data(); draw() }
{% endhighlight %}

### Functions ### {#03.03}

Expand the "x" and "y" boundaries to include all points.

{% highlight awk %}
function expand(note) { if (xmin == "") expand1(note) }
function expand1(note) {
 	xmin = xmax = x[1]    
    ymin = ymax = y[1]
    for (i = 2; i &lt;= nd; i++) {
        if (x[i] &lt; xmin) xmin = x[i]
        if (x[i] > xmax) xmax = x[i]
        if (y[i] &lt; ymin) ymin = y[i]
        if (y[i] > ymax) ymax = y[i] }
}
{% endhighlight %}

Draw the frame around the graph.

{% highlight awk %}
function frame() {        
    for (i = ox; i &lt; wid; i++) plot(i, oy, "-")     # bottom
    for (i = ox; i &lt; wid; i++) plot(i, ht-1, "-")   # top
    for (i = oy; i &lt; ht; i++) plot(ox, i, "|")      # left
    for (i = oy; i &lt; ht; i++) plot(wid-1, i, "|")   # right
}
{% endhighlight %}

Create tick marks for both axes.

{% highlight awk %}
function ticks(    i) {   
    for (i = 1; i &lt;= nb; i++) {
        plot(xscale(bticks[i]), oy, "|")
        splot(xscale(bticks[i])-1, 1, bticks[i])
    }
    for (i = 1; i &lt;= nl; i++) {
        plot(ox, yscale(lticks[i]), "-")
        splot(0, yscale(lticks[i]), lticks[i])
    }
}
{% endhighlight %}

Center labels under x-axis.

{% highlight awk %}
function label() {        
    splot(int((wid + ox - length(botlab))/2), 0, botlab)
}
{% endhighlight %}

Create data points.

{% highlight awk %}
function data(    i) {    
    for (i = 1; i &lt;= nd; i++)
        plot(xscale(x[i]),yscale(y[i]),ch[i]=="" ? "*" : ch[i])
    for(i in mark) print mark[i]
}
{% endhighlight %}

Print graph from array.

{% highlight awk %}
function draw(    i, j) { 
    for (i = ht-1; i >= 0; i--) {
        for (j = 0; j &lt; wid; j++)
            printf((j,i) in array ? array[j,i] : " ")
        printf("\n")
    }
}
{% endhighlight %}

Scale x-values, y-values.

{% highlight awk %}
function xscale(x) {      
    return int((x-xmin)/(xmax-xmin) * (wid-1-ox) + ox + 0.5)
}
function yscale(y) {      
    return int((y-ymin)/(ymax-ymin) * (ht-1-oy) + oy + 0.5)
}
{% endhighlight %}

Put one character into array.

{% highlight awk %}
function plot(x, y, c) {  
    array[x,y] = c
}
{% endhighlight %}

Put string "s" into array.

{% highlight awk %}
function splot(x, y, s,    i, n) { 
    n = length(s)
    for (i = 0; i &lt; n; i++)
        array[x+i, y] = substr(s, i+1, 1)
}
{% endhighlight %}

## Author ## {#04}

This code comes from the original Awk book by Alfred Aho, Peter
Weinberger &  Brian Kernighan and contains some small modifications by
**(TODO: Change link)** [Tim Menzies][1].

[1]: ?who/timm
