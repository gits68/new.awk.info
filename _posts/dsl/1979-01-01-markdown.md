---
layout: default
title: "Markdown.awk"
tags: [dsl, markdown, external_links, TODO]
permalink: /dsl/markdown.html
---

# {{ page.title }}

## Contents

+ [Synopsis](#01)
+ [Download](#02)
+ [Description](#03)
+ [Code](#04)
	+ [Globals](#04.01)
	+ [Images](#04.02)
	+ [Links](#04.03)
	+ [Images](#04.04)
	+ [Emphasis](#04.05)
	+ [Setex-style Headers](#04.06)
	+ [Atx-style Headers](#04.07)
	+ [Unordered Lists](#04.08)
	+ [Paragraphs](#04.09)
	+ [Default](#04.10)
	+ [End](#04.11)
+ [Bugs](#05)
+ [Author](#06)

## Synopsis ## {#01}

{% highlight sh %}
awk -f markdown.awk file.txt > file.html
{% endhighlight %}

## Download ## {#02}

Download from [LAWKER][1].

## Description ## {#03}

(Note: this code was orginally called *txt2html.awk* by its
author but that caused a name clash inside LAWKER. Hence, I've taken
the liberty of renamining it. \-\-**(TODO: Change link)** [Timm][2])

The following code implements a subset of John Gruber's [Markdown][3]
language: a widely-used, ultra light-weight markup language for html.

+ Paragraghs\- denoted by a leading blank line.
+ Images: `![alt text](/path/img.jpg "Title")`
+ Emphasis: `**To be in italics**`
+ Code: \`&lt;code&gt;\` spans are delimited by backticks.
+ Headings (Setex style):

<pre>
Level 1 Header 
=============== 

Level 2 Header
--------------

Level 3 Header 
______________
</pre>

+ Headings (Atx style):  
  Number of leading "\#" codes the heading level:

<pre>
# Level 1 Header
#### Level 4 Header
</pre>

+ Unordered lists

<pre>
- List item 1
- List item 2
</pre>

  Note: beginning and end of list are automatically inferred, maybe
  not always correctly.

+ Ordered lists  
  Denoted by a number at start-of-line.

<pre>
1 A numbered list item
</pre>

## Code ## {#04}

The following code demonstrates a "exception-style" of Awk
programming. Note how all the processing relating to each mark-up tag is
localized (exception, carrying round prior text and environments). The
modularity of the following code should make it easily hackable.

### Globals ### {#04.01}

{% highlight awk %}
BEGIN {
	env = "none";
	text = "";
}
{% endhighlight %}

### Images ### {#04.02}

{% highlight awk %}
/^!\[.+\] *\(.+\)/ {
	split($0, a, /\] *\(/);
	split(a[1], b, /\[/);
	imgtext = b[2];
	split(a[2], b, /\)/);
	imgaddr = b[1];
	print "<p><img src=\"" imgaddr "\" alt=\"" imgtext "\" title=\"\" /></p>\n";
	text = "";
	next;
}
{% endhighlight %}

### Links ### {#04.03}

{% highlight awk %}
/\] *\(/ {
	do {
		na = split($0, a, /\] *\(/);
		split(a[1], b, "[");
		linktext = b[2];
		nc = split(a[2], c, ")");
		linkaddr = c[1];
		text = text b[1] "<a href=\"" linkaddr "\">" linktext "</a>" c[2];
		for(i = 3; i <= nc; i++)
			text = text ")" c[i];
		for(i = 3; i <= na; i++)
			text = text "](" a[i];
		$0 = text;;
		text = "";
	}
	while (na > 2);
}
{% endhighlight %}

### Code ### {#04.04}

{% highlight awk %}
/`/ {
	while (match($0, /`/) != 0) {
		if (env == "code") {
			sub(/`/, "</code>");
			env = pcenv;
		}
		else {
			sub(/`/, "<code>");
			pcenv = env;
			env = "code";
		}
	}
}
{% endhighlight %}

### Emphasis ### {#04.05}

{% highlight awk %}
/\*\*/ {
	while (match($0, /\*\*/) != 0) {
		if (env == "emph") {
			sub(//, "</emph>");
			env = peenv;
		}
		else {
			sub(/\*\*/, "<emph>");
			peenv = env;
			env = "emph";
		}
	}
}
{% endhighlight %}

### Setex-style Headers ### {#04.06}

(Plus h3 with underscores.)

{% highlight awk %}
/^=+$/ {
	print "<h1>" text "</h1>\n";
	text = "";
	next;
}

/^-+$/ {
	print "<h2>" text "</h2>\n";
	text = "";
	next;
}

/^_+$/ {
	print "<h3>" text "</h3>\n";
	text = "";
	next;
}
{% endhighlight %}

### Atx-style headers ### {#04.07}

{% highlight awk %}
/^#/ {
	match($0, /#+/);
	n = RLENGTH;
	if(n > 6)
		n = 6;
	print "<h" n ">" substr($0, RLENGTH + 1) "</h" n ">\n";
	next;
}
{% endhighlight %}

### Unordered Lists ### {#04.08}

{% highlight awk %}
/^[*-+]/ {
	if (env == "none") {
		env = "ul";
		print "<ul>";
	}
	print "<li>" substr($0, 3) "</li>";
	text = "";
	next;
}

/^[0-9]./ {
	if (env == "none") {
		env = "ol";
		print "<ol>";
	}
	print "<li>" substr($0, 3) "</li>";
	next;
}
{% endhighlight %}

### Paragraphs ### {#04.09}

{% highlight awk %}
/^[ t]*$/ {
	if (env != "none") {
		if (text)
			print text;
		text = "";
		print "</" env ">\n";
		env = "none";
	}
	if (text)
		print "<p>" text "</p>\n";
	text = "";
	next;
}
{% endhighlight %}

### Default ### {#04.10}

{% highlight awk %}
// {
	text = text $0;
}
{% endhighlight %}

### End ### {#04.11}

{% highlight awk %}
END {
        if (env != "none") {
                if (text)
                        print text;
                text = "";
                print "</" env ">\n";
                env = "none";
        }
        if (text)
                print "<p>" text "</p>\n";
        text = "";
}
{% endhighlight %}

## Bugs ## {#05}

Does not implement the full Markdown syntax.

## Author ## {#06}

Jesus Galan (yiyus) 2006 <yiyu.jgl@gmail.com>

[1]: http://lawker.googlecode.com/svn/fridge/gawk/text/markdown.awk
[2]: ?who/timm
[3]: http://daringfireball.net/projects/markdown/
