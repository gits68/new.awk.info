---
layout: default
title: Handy One-Liners for Awk (0.22)
tags: [oneline, oneliners, utils]
---

Handy One-Liners For Awk (0.22)
===============================


Eric Pement  
<pemente@northpark.edu>

Latest version of this file is usually at:  
<http://www.student.northpark.edu/pemente/awk/awk1line.txt>

USAGE
-----


    Unix:     awk '/pattern/ {print "$1"}'    # standard Unix shells
    DOS/Win:  awk '/pattern/ {print "$1"}'    # okay for DJGPP compiled
              awk "/pattern/ {print \"$1\"}"  # required for Mingw32

Most of my experience comes from version of GNU awk (gawk) compiled for
Win32. Note in particular that DJGPP compilations permit the awk script to
follow Unix quoting syntax '/like/ {"this"}'. However, the user must know
that single quotes under DOS/Windows do not protect the redirection arrows
(&lt;, >) nor do they protect pipes (|). Both are special symbols for the
DOS/CMD command shell and their special meaning is ignored only if they
are placed within "double quotes." Likewise, DOS/Win users must remember
that the percent sign (%) is used to mark DOS/Win environment variables,
so it must be doubled (%%) to yield a single percent sign visible to awk.

If I am sure that a script will NOT need to be quoted in Unix, DOS, or
CMD, then I normally omit the quote marks. If an example is peculiar
to GNU awk, the command 'gawk' will be used. Please notify me if you
find errors or new commands to add to this list (total length under 65
characters). I usually try to put the shortest script first.

File Spacing
------------


Double space a file

{% highlight awk %}
    awk '1;{print ""}'
    awk 'BEGIN{ORS="\n\n"};1'
{% endhighlight %}

Double space a file which already has blank lines in it. Output file
should contain no more than one blank line between lines of text. NOTE:
On Unix systems, DOS lines which have only CRLF (\r\n) are often treated
as non-blank, and thus 'NF' alone will return TRUE.

{% highlight awk %}
    awk 'NF{print $0 "\n"}'
{% endhighlight %}

Triple space a file

{% highlight awk %}
    awk '1;{print "\n"}'
{% endhighlight %}

Numbering and Calculations
--------------------------


Precede each line by its line number FOR THAT FILE (left alignment).
Using a tab (\t) instead of space will preserve margins.

{% highlight awk %}
    awk '{print FNR "\t" $0}' files*
{% endhighlight %}

Precede each line by its line number FOR ALL FILES TOGETHER, with tab.

{% highlight awk %}
    awk '{print NR "\t" $0}' files*
{% endhighlight %}

Number each line of a file (number on left, right-aligned). Double the
percent signs if typing from the DOS command prompt.

{% highlight awk %}
    awk '{printf("%5d : %s\n", NR,$0)}'
{% endhighlight %}

Number each line of file, but only print numbers if line is not blank.
Remember caveats about Unix treatment of \r (mentioned above).

{% highlight awk %}
    awk 'NF{$0=++a " :" $0};{print}'
    awk '{print (NF? ++a " :" :"") $0}'
{% endhighlight %}

Count lines (emulates "wc -l").

{% highlight awk %}
    awk 'END{print NR}'
{% endhighlight %}

Print the sums of the fields of every line.

{% highlight awk %}
    awk '{s=0; for (i=1; i<=NF; i++) s=s+$i; print s}'
{% endhighlight %}

Add all fields in all lines and print the sum.

{% highlight awk %}
    awk '{for (i=1; i<=NF; i++) s=s+$i}; END{print s}'
{% endhighlight %}

Print every line after replacing each field with its absolute value.

{% highlight awk %}
    awk '{for (i=1; i<=NF; i++) if ($i < 0) $i = -$i; print }'
    awk '{for (i=1; i<=NF; i++) $i = ($i < 0) ? -$i : $i; print }'
{% endhighlight %}

Print the total number of fields ("words") in all lines.

{% highlight awk %}
    awk '{ total = total + NF }; END {print total}' file
{% endhighlight %}

Print the total number of lines that contain "Beth".

{% highlight awk %}
    awk '/Beth/{n++}; END {print n+0}' file
{% endhighlight %}

Print the largest first field and the line that contains it. Intended for
finding the longest string in field #1.

{% highlight awk %}
    awk '$1 > max {max=$1; maxline=$0}; END{ print max, maxline}'
{% endhighlight %}

Print the number of fields in each line, followed by the line.

{% highlight awk %}
    awk '{ print NF ":" $0 } '
{% endhighlight %}

Print the last field of each line.

{% highlight awk %}
    awk '{ print $NF }'
{% endhighlight %}

Print the last field of the last line.

{% highlight awk %}
    awk '{ field = $NF }; END{ print field }'
{% endhighlight %}

Print every line with more than 4 fields.

{% highlight awk %}
    awk 'NF > 4'
{% endhighlight %}

Print every line where the value of the last field is > 4.

{% highlight awk %}
    awk '$NF > 4'
{% endhighlight %}

Text Conversion and Substitution
--------------------------------


IN UNIX ENVIRONMENT: convert DOS newlines (CR/LF) to Unix format

{% highlight awk %}
    awk '{sub(/\r$/,"");print}'   # assumes EACH line ends with Ctrl-M
{% endhighlight %}

IN UNIX ENVIRONMENT: convert Unix newlines (LF) to DOS format

{% highlight awk %}
    awk '{sub(/$/,"\r");print}
{% endhighlight %}

IN DOS ENVIRONMENT: convert Unix newlines (LF) to DOS format

{% highlight awk %}
    awk 1
{% endhighlight %}

IN DOS ENVIRONMENT: convert DOS newlines (CR/LF) to Unix format.
Cannot be done with DOS versions of awk, other than gawk:

{% highlight awk %}
    gawk -v BINMODE="w" '1' infile >outfile
{% endhighlight %}

Use "tr" instead.

{% highlight awk %}
    tr -d \r <infile >outfile # GNU tr version 1.22 or higher
{% endhighlight %}

Delete leading whitespace (spaces, tabs) from front of each line aligns
all text flush left

{% highlight awk %}
    awk '{sub(/^[ \t]+/, ""); print}'
{% endhighlight %}

Delete trailing whitespace (spaces, tabs) from end of each line

{% highlight awk %}
    awk '{sub(/[ \t]+$/, "");print}'
{% endhighlight %}

Delete BOTH leading and trailing whitespace from each line

{% highlight awk %}
    awk '{gsub(/^[ \t]+|[ \t]+$/,"");print}'
    awk '{$1=$1;print}'           # also removes extra space between fields
{% endhighlight %}

Insert 5 blank spaces at beginning of each line (make page offset)

{% highlight awk %}
    awk '{sub(/^/, "     ");print}'
{% endhighlight %}

Align all text flush right on a 79-column width

{% highlight awk %}
    awk '{printf "%79s\n", $0}' file*
{% endhighlight %}

Center all text on a 79-character width

{% highlight awk %}
    awk '{l=length();s=int((79-l)/2); printf "%"(s+l)"s\n",$0}' file*
{% endhighlight %}

Substitute (find and replace) "foo" with "bar" on each line

{% highlight awk %}
    awk '{sub(/foo/,"bar");print}'           # replaces only 1st instance
    gawk '{$0=gensub(/foo/,"bar",4);print}'  # replaces only 4th instance
    awk '{gsub(/foo/,"bar");print}'          # replaces ALL instances in a line
{% endhighlight %}

Substitute "foo" with "bar" ONLY for lines which contain "baz"

{% highlight awk %}
    awk '/baz/{gsub(/foo/, "bar")};{print}'
{% endhighlight %}

Substitute "foo" with "bar" EXCEPT for lines which contain "baz"

{% highlight awk %}
    awk '!/baz/{gsub(/foo/, "bar")};{print}'
{% endhighlight %}

Change "scarlet" or "ruby" or "puce" to "red"

{% highlight awk %}
    awk '{gsub(/scarlet|ruby|puce/, "red"); print}'
{% endhighlight %}

Reverse order of lines (emulates "tac")

{% highlight awk %}
    awk '{a[i++]=$0} END {for (j=i-1; j>=0;) print a[j--] }' file*
{% endhighlight %}

If a line ends with a backslash, append the next line to it
(fails if there are multiple lines ending with backslash...)

{% highlight awk %}
    awk '/\\$/ {sub(/\\$/,""); getline t; print $0 t; next}; 1' file*
{% endhighlight %}

Print and sort the login names of all users

{% highlight awk %}
    awk -F ":" '{ print $1 | "sort" }' /etc/passwd
{% endhighlight %}

Print the first 2 fields, in opposite order, of every line

{% highlight awk %}
    awk '{print $2, $1}' file
{% endhighlight %}

Switch the first 2 fields of every line

{% highlight awk %}
    awk '{temp = $1; $1 = $2; $2 = temp}' file
{% endhighlight %}

Print every line, deleting the second field of that line

{% highlight awk %}
    awk '{ $2 = ""; print }'
{% endhighlight %}

Print in reverse order the fields of every line

{% highlight awk %}
    awk '{for (i=NF; i>0; i--) printf("%s ",i);printf ("\n")}' file
{% endhighlight %}

Remove duplicate, consecutive lines (emulates "uniq")

{% highlight awk %}
    awk 'a !~ $0; {a=$0}'
{% endhighlight %}

Remove duplicate, nonconsecutive lines

{% highlight awk %}
    awk '! a[$0]++'                     # most concise script
    awk '!($0 in a) {a[$0];print}'      # most efficient script
{% endhighlight %}

Concatenate every 5 lines of input, using a comma separator between fields

{% highlight awk %}
    awk 'ORS=%NR%5?",":"\n"' file
{% endhighlight %}

Selective Printing of Certain Lines
-----------------------------------


Print first 10 lines of file (emulates behavior of "head")

{% highlight awk %}
    awk 'NR < 11'
{% endhighlight %}

 Print first line of file (emulates "head -1")

{% highlight awk %}
    awk 'NR>1{exit};1'
{% endhighlight %}

Print the last 2 lines of a file (emulates "tail -2")

{% highlight awk %}
    awk '{y=x "\n" $0; x=$0};END{print y}'
{% endhighlight %}

Print the last line of a file (emulates "tail -1")

{% highlight awk %}
    awk 'END{print}'
{% endhighlight %}

Print only lines which match regular expression (emulates "grep")

{% highlight awk %}
    awk '/regex/'
{% endhighlight %}

Print only lines which do NOT match regex (emulates "grep -v")

{% highlight awk %}
    awk '!/regex/'
{% endhighlight %}

Print the line immediately before a regex, but not the line containing
the regex

{% highlight awk %}
    awk '/regex/{print x};{x=$0}'
    awk '/regex/{print (x=="" ? "match on line 1" : x)};{x=$0}'
{% endhighlight %}

Print the line immediately after a regex, but not the line containing
the regex

{% highlight awk %}
    awk '/regex/{getline;print}'
{% endhighlight %}

Grep for AAA and BBB and CCC (in any order)

{% highlight awk %}
    awk '/AAA/; /BBB/; /CCC/'
{% endhighlight %}

Grep for AAA and BBB and CCC (in that order)

{% highlight awk %}
    awk '/AAA.*BBB.*CCC/'
{% endhighlight %}

Print only lines of 65 characters or longer

{% highlight awk %}
    awk 'length > 64'
{% endhighlight %}

Print only lines of less than 65 characters

{% highlight awk %}
    awk 'length < 64'
{% endhighlight %}

Print section of file from regular expression to end of file

{% highlight awk %}
    awk '/regex/,0'
    awk '/regex/,EOF'
{% endhighlight %}

Print section of file based on line numbers (lines 8-12, inclusive)

{% highlight awk %}
    awk 'NR==8,NR==12'
{% endhighlight %}

Print line number 52

{% highlight awk %}
    awk 'NR==52'
    awk 'NR==52 {print;exit}'          # more efficient on large files
{% endhighlight %}

Print section of file between two regular expressions (inclusive)

{% highlight awk %}
    awk '/Iowa/,/Montana/'             # case sensitive
{% endhighlight %}

Selective Deletion of Certain Lines
-----------------------------------


Delete ALL blank lines from a file (same as "grep '.' ")

{% highlight awk %}
    awk NF
    awk '/./'
{% endhighlight %}

Credits and Thanks
------------------


Special thanks to Peter S. Tillier for helping me with the first release
of this FAQ file.

For additional syntax instructions, including the way to apply editing
commands from a disk file instead of the command line, consult:

+ "sed & awk, 2nd Edition," by Dale Dougherty and Arnold Robbins O'Reilly, 1997
+ "UNIX Text Processing," by Dale Dougherty and Tim O'Reilly Hayden Books, 1987
+ "Effective awk Programming, 3rd Edition." by Arnold Robbins O'Reilly, 2001

To fully exploit the power of awk, one must understand "regular
expressions." For detailed discussion of regular expressions, see

+ "Mastering Regular Expressions, 2d edition" by Jeffrey Friedl (O'Reilly, 2002).

The manual ("man") pages on Unix systems may be helpful (try "man awk",
"man nawk", "man regexp", or the section on regular expressions in "man
ed"), but man pages are notoriously difficult. They are not written to
teach awk use or regexps to first-time users, but as a reference text
for those already acquainted with these tools.

USE OF '\t' IN awk SCRIPTS: For clarity in documentation, we have used
the expression '\t' to indicate a tab character (0x09) in the scripts.
All versions of awk, even the UNIX System 7 version should recognize the
'\t' abbreviation.
